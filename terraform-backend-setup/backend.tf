# Provider configuration
provider "aws" {
    region = "us-east-1"
}

#S3 bucket for storing state
resource "aws_s3_bucket" "terraform_state"{
    bucket = "terraform-state-alsuppes-2025" #This name must be unique

    tags = {
        Name = "Terraform State Bucket"
        Environment = "Learning"
    }
}

# Enable versioning to recover old state versions
resource "aws_s3_bucket_versioning" "terraform_state"{
    bucket = aws_s3_bucket.terraform_state.id

    versioning_configuration {
        status = "Enabled"
    }
}

# Enable encryption at rest
resource "aws_s3_bucket_server_side_encryption_configuration" "terraform_state" {
    bucket = aws_s3_bucket.terraform_state.id

    rule {
        apply_server_side_encryption_by_default {
            sse_algorithm = "AES256"
        }
    }
}

#Block public access
resource "aws_s3_bucket_public_access_block" "terraform_state" {
    bucket = aws_s3_bucket.terraform_state.id
    
    block_public_acls = true
    block_public_policy = true
    ignore_public_acls = true
    restrict_public_buckets = true
}

#DynamoDB table for state locking
resource "aws_dynamodb_table" "terraform_locks" {
    name = "terraform-state-locks"
    billing_mode = "PAY_PER_REQUEST"
    hash_key = "LockID"

    attribute {
        name = "LockID"
        type = "S"
    }

    tags = {
        Name = "Terraform State Lock Table"
        Environment = "Learning"
    }

}